# Maintainer: otaj <jasek.ota@gmail.com>
# If you want the python package to work with MKL, you need to have python-numpy-mkl installed!
# See https://github.com/facebookresearch/faiss/issues/1393

_pkgname=faiss
pkgbase=faiss
pkgname=('faiss-cuda' 'python-faiss-cuda')
arch=('i686' 'x86_64')
url="https://github.com/facebookresearch/faiss"
license=('MIT')
pkgver=1.7.0
pkgrel=2
source=(${_pkgname}::git+https://github.com/facebookresearch/faiss.git#tag=v${pkgver}
    cu.patch )
sha256sums=('SKIP' 'SKIP')
depends=('blas' 'lapack' 'openmp' 'cuda' 'intel-mkl' )
makedepends=('git' 'python' 'python-numpy' 'swig' 'python-setuptools' 'cmake')
optdepends=('intel-mkl: To use MKL blas implemenetation' 'python-numpy-mkl: To use MKL blas implementation.')


prepare() {
  patch -p0 < cu.patch
  cd "${srcdir}/${_pkgname}"
  mkdir -p build
  cd build
  cmake .. \
    -DFAISS_ENABLE_GPU=ON \
    -DFAISS_ENABLE_PYTHON=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DFAISS_OPT_LEVEL=avx2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUDAToolkit_ROOT=/opt/cuda \
    -DCMAKE_CUDA_ARCHITECTURES="86" \
    -DCMAKE_C_COMPILER=/usr/bin/gcc-10 \
    -DCMAKE_CXX_COMPILER=/usr/bin/g++-10 \
    -DBUILD_TESTING=ON
    # -DMKL_LIBRARIES=/opt/intel/mkl -DBLA_VENDOR=Intel10_64ilp \
}


build() {
  cd "${srcdir}/${_pkgname}/build"
  make -C . -j12 faiss swigfaiss
  cd faiss/python
  python setup.py build
}

package_faiss-cuda() {
  pkgdesc='A library for efficient similarity search and clustering of dense vectors.'
  provides=('faiss')
  conflicts=('faiss')
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="$pkgdir" install
  cd "${pkgdir}"/usr/lib
  ln -fs libfaiss_avx2.a libfaiss.a
  cd "${srcdir}/${_pkgname}/build"
  cd ..
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-faiss-cuda() {
  pkgdesc='A library for efficient similarity search and clustering of dense vectors. Python wrappers'
  provides=('python-faiss')
  conflicts=('python-faiss')
  depends=('python' 'python-numpy' 'faiss-cuda')

  cd "${srcdir}/${_pkgname}/build/faiss/python"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  cd ../../..
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

